#
# ngIRCd -- The Next Generation IRC Daemon
# Copyright (c)2001-2003 by Alexander Barton (alex@barton.de)
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
# Please read the file COPYING, README and AUTHORS for more information.
#
# $Id: configure.in,v 1.89.2.5 2003/04/27 18:33:26 alex Exp $
#

# -- Initialisierung --

AC_PREREQ(2.50)
AC_INIT(ngircd, 0.7.0-pre2)
AC_CONFIG_SRCDIR(src/ngircd/ngircd.c)
AC_CANONICAL_TARGET
AM_INIT_AUTOMAKE(1.6)
AM_CONFIG_HEADER(src/config.h)

# -- Templates fuer config.h --

AH_TEMPLATE([DEBUG], [Define if debug-mode should be enabled])
AH_TEMPLATE([HAVE_socklen_t], [Define if socklen_t exists])
AH_TEMPLATE([SNIFFER], [Define if IRC sniffer should be enabled])
AH_TEMPLATE([STRICT_RFC], [Define if ngIRCd should behave strict RFC compliant])
AH_TEMPLATE([USE_SYSLOG], [Define if syslog should be used for logging])
AH_TEMPLATE([USE_ZLIB], [Define if zlib compression should be enabled])
AH_TEMPLATE([USE_TCPWRAP], [Define if TCP wrappers should be used])
AH_TEMPLATE([IRCPLUS], [Define if IRC+ protocol should be used])
AH_TEMPLATE([RENDEZVOUS], [Define if Rendezvous support should be included])

AH_TEMPLATE([TARGET_OS], [Target operating system name])
AH_TEMPLATE([TARGET_VENDOR], [Target system vendor])
AH_TEMPLATE([TARGET_CPU], [Target CPU name])

# -- C Compiler --

AC_PROG_CC

# -- Hilfsprogramme --

AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

# -- Compiler Features --

AC_LANG_C

AM_C_PROTOTYPES
AC_C_CONST

# -- Defines --

if test `uname` = "Linux"; then
	# define _POSIX_SOURCE, _GNU_SOURCE and _BSD_SOURCE when compiling
	# on Linux (glibc-based systems):
	AC_MSG_RESULT([detected Linux, defining _POSIX_SOURCE, _GNU_SOURCE and _BSD_SOURCE])
	add_DEFINES="-D_POSIX_SOURCE -D_GNU_SOURCE -D_BSD_SOURCE $add_DEFINES"
fi

if test `uname` = "A/UX"; then
	# define _POSIX_SOURCE when compiling on A/UX:
	AC_MSG_RESULT([detected A/UX, defining _POSIX_SOURCE])
	add_DEFINES="-D_POSIX_SOURCE $add_DEFINES"
fi

if test `uname` = "HP-UX"; then
	# define _XOPEN_SOURCE_EXTENDED when compiling on HP-UX (11.11):
	AC_MSG_RESULT([detected HP-UX, defining _XOPEN_SOURCE_EXTENDED])
	add_DEFINES="-D_XOPEN_SOURCE_EXTENDED $add_DEFINES"
fi

if test `uname` = "SunOS"; then
	# define _XOPEN_SOURCE, _XOPEN_SOURCE_EXTENDED=1 and __EXTENSIONS__
	# when compiling on SunOS (tested with 5.6):
	AC_MSG_RESULT([detected SunOS, defining _XOPEN_SOURCE, _XOPEN_SOURCE_EXTENDED=1 and __EXTENSIONS__])
	add_DEFINES="-D_XOPEN_SOURCE -D_XOPEN_SOURCE_EXTENDED=1 -D__EXTENSIONS__ $add_DEFINES"
fi

# -- Header --

AC_HEADER_STDC

AC_HEADER_TIME

AC_HEADER_SYS_WAIT

AC_CHECK_HEADERS([ \
	ctype.h errno.h fcntl.h netdb.h netinet/in.h stdlib.h string.h \
	strings.h sys/socket.h sys/time.h unistd.h \
	],,AC_MSG_ERROR([required C header missing!]))

AC_CHECK_HEADERS([arpa/inet.h ctype.h malloc.h stdint.h varargs.h])

# -- Datentypen --

AC_MSG_CHECKING(whether socklen_t exists)
AC_TRY_COMPILE([
	#include <sys/socket.h>
	#include <sys/types.h>
	],[
	socklen_t a, b;
	a = 2; b = 4; a += b;
	],[
	AC_DEFINE(HAVE_socklen_t) AC_MSG_RESULT(yes)
	],[
	AC_MSG_RESULT(no)
])

AC_TYPE_SIGNAL

AC_TYPE_SIZE_T

# -- Libraries --

AC_CHECK_LIB(UTIL,memmove)
AC_CHECK_LIB(socket,bind)
AC_CHECK_LIB(nsl,gethostent)

# -- Funktionen --

AC_FUNC_MALLOC

AC_FUNC_FORK

AC_FUNC_STRFTIME

AC_CHECK_FUNCS([ \
	bind gethostbyaddr gethostbyname gethostname inet_ntoa memmove \
	memset setsockopt socket strcasecmp strchr strerror strstr waitpid \
	],,AC_MSG_ERROR([required function missing!]))

AC_CHECK_FUNCS(inet_aton isdigit sigaction snprintf vsnprintf strlcpy strlcat)

AC_CHECK_FUNCS(select,[AC_CHECK_HEADERS(sys/select.h)],
	AC_MSG_ERROR([required function select() is missing!])
)

# -- Konfigurationsoptionen --

x_syslog_on=no
AC_ARG_WITH(syslog,
	[  --without-syslog        disable syslog (autodetected by default)],
	[	if test "$withval" = "yes"; then
			AC_CHECK_LIB(be, syslog)
			AC_CHECK_FUNCS(syslog, x_syslog_on=yes,
				AC_MSG_ERROR([Can't enable syslog!])
			)
		fi
	],
	[
		AC_CHECK_LIB(be, syslog)
		AC_CHECK_FUNCS(syslog, x_syslog_on=yes)
	]
)
if test "$x_syslog_on" = "yes"; then
	AC_DEFINE(USE_SYSLOG, 1)
	AC_CHECK_HEADERS(syslog.h)
fi

x_zlib_on=no
AC_ARG_WITH(zlib,
	[  --without-zlib          disable zlib compression (autodetected by default)],
	[	if test "$withval" = "yes"; then
			AC_CHECK_LIB(z, deflate)
			AC_CHECK_FUNCS(deflate, x_zlib_on=yes,
				AC_MSG_ERROR([Can't enable zlib!])
			)
		fi
	],
	[	AC_CHECK_LIB(z, deflate)
		AC_CHECK_FUNCS(deflate, x_zlib_on=yes)
	]
)
if test "$x_zlib_on" = "yes"; then
	AC_DEFINE(USE_ZLIB, 1)
	AC_CHECK_HEADERS(zlib.h)
fi

x_tcpwrap_on=no
AC_ARG_WITH(tcp-wrappers,
	[  --with-tcp-wrappers     enable TCP wrappers support],
	[	if test "$withval" = "yes"; then
			AC_CHECK_LIB(wrap, tcpd_warn)
			AC_MSG_CHECKING(for hosts_access)
			AC_TRY_LINK([
				#include <tcpd.h>
				],[
				void *ptr;
				ptr = hosts_access;
				],[
				AC_MSG_RESULT(yes)
				AC_DEFINE(USE_TCPWRAP, 1)
				x_tcpwrap_on=yes
				],[
				AC_MSG_RESULT(no)
				AC_MSG_ERROR([Can't enable TCP wrappers!])
			])
		fi
	]
)

x_rendezvous_on=no
AC_ARG_WITH(rendezvous,
	[  --with-rendezvous       enable support for "Rendezvous"],
	[	if test "$withval" = "yes"; then
			AC_CHECK_FUNCS(DNSServiceRegistrationCreate, x_rendezvous_on=yes,
				AC_MSG_ERROR([Can't enable Rendezvous!])
			)
		fi
	]
)
if test "$x_rendezvous_on" = "yes"; then
	AC_DEFINE(RENDEZVOUS, 1)
	AC_CHECK_HEADERS(DNSServiceDiscovery/DNSServiceDiscovery.h mach/port.h)
fi

x_ircplus_on=yes
AC_ARG_ENABLE(ircplus,
	[  --disable-ircplus       disable IRC+ protocol],
	if test "$enableval" = "no"; then x_ircplus_on=no; fi
)
if test "$x_ircplus_on" = "yes"; then
	AC_DEFINE(IRCPLUS, 1)
fi

x_sniffer_on=no; x_debug_on=no
AC_ARG_ENABLE(sniffer,
	[  --enable-sniffer        enable IRC traffic sniffer (enables debug mode)],
	if test "$enableval" = "yes"; then
		AC_DEFINE(SNIFFER, 1)
		x_sniffer_on=yes; x_debug_on=yes
	fi
)

AC_ARG_ENABLE(debug,
	[  --enable-debug          show additional debug output],
	if test "$enableval" = "yes"; then x_debug_on=yes; fi
)
if test "$x_debug_on" = "yes"; then
	AC_DEFINE(DEBUG, 1)
fi

x_strict_rfc_on=no
AC_ARG_ENABLE(strict-rfc,
	[  --enable-strict-rfc     strict RFC conformance -- may break clients!],
	if test "$enableval" = "yes"; then
		AC_DEFINE(STRICT_RFC, 1)
		x_strict_rfc_on=yes
	fi
)

# -- Definitionen --

AC_DEFINE_UNQUOTED(TARGET_CPU, "$target_cpu" )
AC_DEFINE_UNQUOTED(TARGET_VENDOR, "$target_vendor" )
AC_DEFINE_UNQUOTED(TARGET_OS, "$target_os" )

# -- Variablen --

if test "$GCC" = "yes"; then
	the_CFLAGS="-Wmissing-declarations -Wpointer-arith -Wstrict-prototypes"
	ansi=" -ansi"
	pedantic=" -pedantic"

	$CC --version | grep 20020420 > /dev/null 2>&1
	if test $? -eq 0; then
		# Mac OS X (and Darwin?) ship with a slightly broken
		# prerelease of GCC 3.1 which don't like -pedantic:
		AC_MSG_RESULT([detected broken GNU C compiler, disabling "-pedantic"])
		pedantic=""
	fi

	uname | grep "CYGWIN" > /dev/null 2>&1
	if test $? -eq 0; then
		# The include files of Cygwin don't like -ansi,
		# so we disable it:
		AC_MSG_RESULT([detected Cygwin, disabling "-ansi"])
		ansi=""
	fi

	add_CFLAGS="-Wall -W${ansi}${pedantic} $CFLAGS $CFLAGS_ADD"
else
	the_CFLAGS="$CFLAGS"
	add_CFLAGS="$CFLAGS_ADD"
fi

CFLAGS="$the_CFLAGS $add_CFLAGS $add_DEFINES -DSYSCONFDIR='\"\$(sysconfdir)\"'"

# -- Ausgabe der Dateien --

AC_OUTPUT([ \
	Makefile \
	doc/Makefile \
	MacOSX/Makefile \
	MacOSX/ngircd.pbproj/Makefile \
	src/Makefile \
	src/portab/Makefile \
	src/tool/Makefile \
	src/ngircd/Makefile \
	src/testsuite/Makefile \
	man/Makefile \
	contrib/Makefile \
])

# -- Result --

echo
echo "ngIRCd has been configured with the following options:"
echo

# Someone please show me a better way :)  [borrowed by OpenSSH]
B=`eval echo ${bindir}` ; B=`eval echo ${B}`
S=`eval echo ${sbindir}` ; S=`eval echo ${S}`
C=`eval echo ${sysconfdir}` ; C=`eval echo ${C}`
M=`eval echo ${mandir}` ; M=`eval echo ${M}`
D=`eval echo ${datadir}/doc/${PACKAGE}` ; D=`eval echo ${D}`

echo "               Host: ${host}"
echo "           Compiler: ${CC}"
echo "     Compiler flags: ${the_CFLAGS}"
test -n "$add_CFLAGS"	&& echo "                     ${add_CFLAGS}"
test -n "$add_DEFINES"	&& echo "                     ${add_DEFINES}"
test -n "$CPPFLAGS"	&& echo " Preprocessor flags: ${CPPFLAGS}"
test -n "$LDFLAGS"	&& echo "       Linker flags: ${LDFLAGS}"
test -n "$LIBS"		&& echo "          Libraries: ${LIBS}"
echo
echo "    'ngircd' binary: $S"
echo " Configuration file: $C"
echo "       Manual pages: $M"
echo "      Documentation: $D"
echo

echo $ECHO_N "     Syslog support: $ECHO_C"
test "$x_syslog_on" = "yes" \
	&& echo $ECHO_N "yes $ECHO_C" \
	|| echo $ECHO_N "no  $ECHO_C"
echo $ECHO_N "  Enable debug code: $ECHO_C"
test "$x_debug_on" = "yes" \
	&& echo "yes" \
	|| echo "no"

echo $ECHO_N "   zlib compression: $ECHO_C"
test "$x_zlib_on" = "yes" \
	&& echo $ECHO_N "yes $ECHO_C" \
	|| echo $ECHO_N "no  $ECHO_C"
echo $ECHO_N "        IRC sniffer: $ECHO_C"
test "$x_sniffer_on" = "yes" \
	&& echo "yes" \
	|| echo "no"

echo $ECHO_N "   Use TCP Wrappers: $ECHO_C"
test "$x_tcpwrap_on" = "yes" \
	&& echo $ECHO_N "yes $ECHO_C" \
	|| echo $ECHO_N "no  $ECHO_C"
echo $ECHO_N "    Strict RFC mode: $ECHO_C"
test "$x_strict_rfc_on" = "yes" \
	&& echo "yes" \
	|| echo "no"

echo $ECHO_N " Rendezvous support: $ECHO_C"
test "$x_rendezvous_on" = "yes"	\
	&& echo $ECHO_N "yes $ECHO_C" \
	|| echo $ECHO_N "no  $ECHO_C"
echo $ECHO_N "      IRC+ protocol: $ECHO_C"
test "$x_ircplus_on" = "yes" \
	&& echo "yes" \
	|| echo "no"
echo

# -eof-
