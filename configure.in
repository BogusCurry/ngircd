#
# ngIRCd -- The Next Generation IRC Daemon
# Copyright (c)2001,2002 by Alexander Barton (alex@barton.de)
#
# Dieses Programm ist freie Software. Sie koennen es unter den Bedingungen
# der GNU General Public License (GPL), wie von der Free Software Foundation
# herausgegeben, weitergeben und/oder modifizieren, entweder unter Version 2
# der Lizenz oder (wenn Sie es wuenschen) jeder spaeteren Version.
# Naehere Informationen entnehmen Sie bitter der Datei COPYING. Eine Liste
# der an ngIRCd beteiligten Autoren finden Sie in der Datei AUTHORS.
#
# $Id: configure.in,v 1.39.2.5 2002/06/11 20:29:44 alex Exp $
#

# -- Initialisierung --

AC_INIT
AC_CANONICAL_TARGET
AC_CONFIG_SRCDIR(src/config.h.in)
AM_INIT_AUTOMAKE(ngircd,0.4.3)
AM_CONFIG_HEADER(src/config.h)

# -- Templates fuer config.h --

AH_TEMPLATE([DEBUG], [Define if debug-mode should be enabled])
AH_TEMPLATE([HAVE_socklen_t], [Define if socklen_t exists])
AH_TEMPLATE([SNIFFER], [Define if IRC sniffer should be enabled])
AH_TEMPLATE([STRICT_RFC], [Define if ngIRCd should behave strict RFC compliant])
AH_TEMPLATE([USE_SYSLOG], [Define if syslog should be used for logging])

AH_TEMPLATE([TARGET_OS], [Target operating system name])
AH_TEMPLATE([TARGET_VENDOR], [Target system vendor])
AH_TEMPLATE([TARGET_CPU], [Target CPU name])

# -- C Compiler --

AC_PROG_CC
AC_LANG_C

# -- Hilfsprogramme --

AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

# -- Header --

AC_HEADER_STDC

AC_HEADER_TIME

AC_CHECK_HEADERS(arpa/inet.h)

AC_CHECK_HEADERS([ \
	errno.h fcntl.h netdb.h netinet/in.h stdlib.h string.h \
	sys/socket.h sys/time.h sys/wait.h unistd.h \
	],,AC_MSG_ERROR([required C header missing!]))

# -- Datentypen --

AC_MSG_CHECKING(whether socklen_t exists)
AC_TRY_COMPILE([
	#include <sys/socket.h>
	#include <sys/types.h>
	],[
	socklen_t a, b;
	a = 2; b = 4; a += b;
	],[
	AC_DEFINE(HAVE_socklen_t) AC_MSG_RESULT(yes)
	],[
	AC_MSG_RESULT(no)
])

# -- Libraries --

AC_CHECK_LIB(UTIL,memmove)

# -- Funktionen --

AC_FUNC_MALLOC

AC_CHECK_FUNCS([ \
	bind gethostbyaddr gethostbyname gethostname inet_ntoa memmove \
	memset select setsockopt socket strcasecmp strchr strerror strftime \
	strstr vsnprintf waitpid \
	],,AC_MSG_ERROR([required function missing!]))

AC_CHECK_FUNCS(inet_aton)

AC_CHECK_FUNCS(sigaction)

# -- Konfigurationsoptionen --

AC_ARG_ENABLE(syslog,
	[  --disable-syslog        disable syslog (autodetected by default)],
	[	if test "$enableval" = "yes"; then
			AC_CHECK_HEADER(syslog.h,
				[	AC_DEFINE(USE_SYSLOG, 1)
					AC_CHECK_LIB(be,syslog)
				],
				AC_MSG_ERROR([Can't enable syslog: syslog.h not found!])
			)
		else
			AC_MSG_RESULT([disabling syslog])
		fi
	],
	[	AC_CHECK_HEADER(syslog.h,
			[	AC_DEFINE(USE_SYSLOG, 1)
				AC_CHECK_LIB(be,syslog)
			]
		)
	]
)

AC_ARG_ENABLE(strict-rfc,
	[  --enable-strict-rfc     strict RFC conformance, may break clients],
	if test "$enableval" = "yes"; then
		AC_DEFINE(STRICT_RFC, 1)
		AC_MSG_RESULT([enabling strict RFC conformance])
	fi
)

AC_ARG_ENABLE(sniffer,
	[  --enable-sniffer        enable network traffic monitor (enables debug mode!)],
	if test "$enableval" = "yes"; then
		AC_DEFINE(SNIFFER, 1)
		AC_MSG_RESULT([enabling network traffic monitor])
		x_debug_on=yes
	fi
)

AC_ARG_ENABLE(debug,
	[  --enable-debug          show additional debug output],
	if test "$enableval" = "yes"; then x_debug_on=yes; fi
)
if test "$x_debug_on" = "yes"; then
	AC_DEFINE(DEBUG, 1)
	AC_MSG_RESULT([enabling additional debug output])
fi

# -- Definitionen --

AC_DEFINE_UNQUOTED(TARGET_CPU, "$target_cpu" )
AC_DEFINE_UNQUOTED(TARGET_VENDOR, "$target_vendor" )
AC_DEFINE_UNQUOTED(TARGET_OS, "$target_os" )

# -- Variablen --

if test "$GCC" = "yes"; then
	CFLAGS="-Wall $CFLAGS"
fi

CFLAGS="$CFLAGS -DSYSCONFDIR='\"\$(sysconfdir)\"'"

if test `uname` = "A/UX"; then
	# unter A/UX sollte _POSIX_SOURCE definiert sein.
	AC_MSG_RESULT([detected A/UX, defining _POSIX_SOURCE])
	CFLAGS="$CFLAGS -D_POSIX_SOURCE"
fi

# -- Ausgabe --

AC_OUTPUT([ \
	Makefile \
	doc/Makefile \
	MacOSX/Makefile \
	MacOSX/ngircd.pbproj/Makefile \
	src/Makefile \
	src/portab/Makefile \
	src/ngircd/Makefile \
])

# -eof-
